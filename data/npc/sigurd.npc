# GIMUD - Graphical Interface Multi User Dungeon
# sigurd.npc: Datenbank für den Händler Sigurd

Name = "Sigurd"
Outfit = (69,0-0-0-0-0)
Home = [32626,31923,5]
Radius = 8

Behaviour = {
ADDRESS,"hello$",! -> "Welcome to the magic store, %N!"
ADDRESS,"hi$",!    -> *
ADDRESS,"hiho$",!  -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "You next %N, jawoll!", Queue
BUSY,"hi$",!       -> *
BUSY,"hiho$",!     -> *
BUSY,!             -> NOP
VANISH,!           -> "Good bye. Come back soon."

"bye"           -> "Good bye. Come back soon.", Idle
"farewell"      -> *

"name"      -> "I am Sigurd Fireworker, brother to Etzel Fireworker, son of fire, of the Molten Rocks."
"job"       -> "I help my brother handling his little magic store so he can focus on studying spells."
"time"      -> "It's %T right now."

"offer"           -> "I'm selling life and mana fluids, runes, wands, rods and spellbooks."
"goods"           -> *
"do","you","sell" -> *
"do","you","have" -> *


"rune"         -> "I sell blank runes."
"life","fluid" -> Type=2874, Data=11, Amount=1, Price=60, "Do you want to buy life fluid for %P gold?", Topic=5
"mana","fluid" -> Type=2874, Data=10, Amount=1, Price=55, "Do you want to buy mana fluid for %P gold?", Topic=5
"blank","rune" -> Type=3147, Amount=1, Price=10, "Do you want to buy a blank rune for %P gold?", Topic=4
"spellbook"    -> Type=3059, Amount=1, Price=150, "In a spellbook, your spells are listed. There you will find the pronunciation of each spell. Do you want to buy one for %P gold?", Topic=4 

"backpack","life","fluid" -> Type=2874, Data=11, Amount=1, Price=61*20, "Do you want to buy a backpack of life fluid for %P gold?", Topic=8
"bp","life","fluid" -> *
"backpack","mana","fluid" -> Type=2874, Data=10, Amount=1, Price=56*20, "Do you want to buy a backpack of mana fluid for %P gold?", Topic=8
"bp","mana","fluid" -> *
"backpack","blank","rune" -> Type=3147, Amount=1, Price=11*20, "Do you want to buy a backpack of blank rune for %P gold?", Topic=7
"bp","blank","rune" -> *

%1,1<%1,"life","fluid"  -> Type=2874, Data=11, Amount=%1, Price=60*%1, "Do you want to buy %A potions of life fluid for %P gold?", Topic=5 
%1,1<%1,"mana","fluid"  -> Type=2874, Data=10, Amount=%1, Price=55*%1, "Do you want to buy %A potions of mana fluid for %P gold?", Topic=5 
%1,1<%1,"blank","rune"  -> Type=3147, Amount=%1, Price=10*%1, "Do you want to buy %A blank runes for %P gold?", Topic=4 
%1,1<%1,"spellbook"     -> Type=3059, Amount=%1, Price=150*%1, "In a spellbook, your spells are listed. There you will find the pronunciation of each spell. Do you want to buy %A spellbooks for %P gold?", Topic=4 
 
%1,1<%1,"backpack","life","fluid"  -> Type=2874, Data=11, Amount=%1, Price=61*20*%1, "Do you want to buy %A backpacks of potions of life fluid for %P gold?", Topic=8 
%1,1<%1,"bp","life","fluid"  -> *
%1,1<%1,"backpack","mana","fluid"  -> Type=2874, Data=10, Amount=%1, Price=56*20*%1, "Do you want to buy %A backpacks of potions of mana fluid for %P gold?", Topic=8 
%1,1<%1,"bp","mana","fluid"  -> *
%1,1<%1,"backpack","blank","rune"  -> Type=3147, Amount=%1, Price=11*20*%1, "Do you want to buy %A backpacks of blank runes for %P gold?", Topic=7 
%1,1<%1,"bp","blank","rune"  -> *
 
"deposit" -> "I will pay you 5 gold for every empty vial. Ok?", Data=0, Topic=6 
"vial"    -> *
"flask"   -> * 

@"gen-t-wands-free-s.ndb"
 
Topic=4,"yes",CountMoney>=Price -> "Here you are.", DeleteMoney, Create(Type)
Topic=4,"yes"                   -> "Come back, when you have enough money."
Topic=4                         -> "Hmm, but next time."

Topic=5,"yes",CountMoney>=Price -> "Here you are. There is a deposit of 5 gold on the vial.", DeleteMoney, Create(Type)
Topic=5,"yes"                   -> "Come back, when you have enough money."
Topic=5                         -> "Hmm, but next time."

Topic=6,"yes",Count(2874)>0 -> Amount=Count(2874), Price=Amount*5, "Here you are ... %P gold.", Delete(2874), CreateMoney
Topic=6,"yes"               -> "You don't have any empty vials."
Topic=6                     -> "Hmm, but please keep Tibia litter free."

Topic=7,"yes",CountMoney>=Price -> "Here you are.", DeleteMoney, CreateContainer(2854,Type,Data)
Topic=7,"yes"                   -> "Come back, when you have enough money."
Topic=7                         -> "Hmm, but next time."

Topic=8,"yes",CountMoney>=Price -> "Here you are. There is a deposit of 5 gold on the vial.", DeleteMoney, CreateContainer(2854,Type,Data)
Topic=8,"yes"                   -> "Come back, when you have enough money."
Topic=8                         -> "Hmm, but next time."

}
